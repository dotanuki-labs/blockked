apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

android {

    compileSdkVersion AndroidConfig.compileSdk
    testOptions.unitTests.includeAndroidResources = true

    def currentVersion = Versioning.getVersion()

    defaultConfig {
        applicationId AndroidConfig.applicationId
        minSdkVersion AndroidConfig.minSdk
        targetSdkVersion AndroidConfig.targetSdk
        versionCode currentVersion.code
        versionName currentVersion.name
        testInstrumentationRunner AndroidConfig.instrumentationTestRunner

        vectorDrawables.useSupportLibrary = true
        vectorDrawables.generatedDensities = []

        archivesBaseName = "app-${currentVersion.name}"
    }

    signingConfigs {
        release {
            storeFile file("../dotanuki-demos.jks")
            storePassword "dotanuki"
            keyAlias 'dotanuki-alias'
            keyPassword "dotanuki"
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            lintOptions {
                tasks.lint.enabled = false
            }

        }

        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles file('../proguard').listFiles().toList().toArray()
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }

    }
}

dependencies {

    implementation project(':logger')
    implementation project(':feature-dashboards')

    AndroidModule.main.forEach { implementation it }
    AndroidModule.unitTesting.forEach { testImplementation it }
    AndroidModule.androidTesting.forEach { androidTestImplementation it }

    androidTestImplementation project(':domain')
    androidTestImplementation project(':features-common')

}

androidExtensions {
    experimental = true
}